################################################################################
#
#Name: gates.praat
#
#Purpose: This script creates a series of "gates" given the selected
# series of Sound objects
#
#Usage: To use this script you must first:
#	1. Create a TextGrid Object for a particular sound
#	2. Create and label intervals for that Sound and TextGrid pair
# 	3. Extract all intervals from the Sound and TextGrid pair
#	4. Select all generated Sound objects that you would like to be combined
#	5. Either run this script directly
#	   or create a button that links to the dynamic menu for Sound objects
# 
#
#Contributors: S. Ganci, K. Elhajoui
#
################################################################################


#Store a reference to the selected Sound objects
sounds#= selected# ("Sound")

#Create the first gate by selecting the first sound and copying it
selectObject: sounds# [1]
Copy: "gate_1"

#Create TextGrid for that first gate
selectObject: "Sound gate_1"
To TextGrid: "gate_1", ""

for i from 2 to size (sounds#)
	selectObject: sounds# [1]
	for j from 2 to i
		plusObject: sounds#[j]
	endfor
	#Concatenate all selected objects
	Concatenate recoverably
	
	#Select newly generated "Sound chain"
	selectObject: "Sound chain"

	#Rename the Sound to "gate_#"
	#The number of the gate should be one less than i
	#gate_number=i-1
	#gate_name$ will store the "gate_#"
	gate_name$= "gate_"+string$(i)
	Rename: gate_name$
	
	#Select the newly generated "TextGrid chain"
	selectObject: "TextGrid chain"

	#Rename the TextGrid to "gate_#"
	Rename: gate_name$
endfor